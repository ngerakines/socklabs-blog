<!DOCTYPE html>
<html>
  <head>
	<link href="/css/style.css" media="screen" rel="stylesheet" type="text/css" />
    <title>Nick's Blog | Dynamically sizing a fragmented mnesia store
</title>
  </head>
  <body>
	<a href="/"><h1>Nick's Blog</h1></a>
	<div id="content">
		<div id="posts">
			<h2><a alt="Dynamically sizing a fragmented mnesia store
" href="/2008/02/06/dynamically_sizing_a_fragmente.html">Dynamically sizing a fragmented mnesia store
</a></h2>
			<em id="sub_h2">06 February 2008</em>
		 	<p>Not too long ago Mark Zweifel introduced me to Erlang. There are plenty of websites that explain what it is, how it works and where it came from so I'm not going to go into those details right here.</p>

<p>What I've really been having fun with is mnesia, the native data storage application. With it you can do some really powerful things like advanced data distribution, federation, complex queriers, full/compartmental replication and the list goes on and on.</p>

<p>The documentation is pretty good and Joe Armstrong's book <a href="http://www.pragprog.com/titles/jaerlang">Programming Erlang: Software for a Concurrent World</a> has a huge collection of example snippets putting many of the concepts in action. What isn't covered includes a lot of the advanced mnesia subjects. These include fragmented tables, multi-node datastores and events.</p>

<p>So over the past few days I've been plowing through a lot of this and figuring out how it works. The first on the was federated/fragmented tables.</p>

<p>Consider a grid of three erlang nodes connected to each other with a common cookie. For the sake of this example each node lives in its own directory on the same server, but this example could apply to nodes on more than one machine. In this example the hostname is set to 'icedcoffee'.</p>

<pre><code>ic:~/tmp/erl1 $ erl -sname node1 -setcookie 622c84b69ee448a07d80de5cbeb13e3d

ic:~/tmp/erl2 $ erl -sname node2 -setcookie 622c84b69ee448a07d80de5cbeb13e3d

ic:~/tmp/erl3 $ erl -sname node3 -setcookie 622c84b69ee448a07d80de5cbeb13e3dTo create our setup we want to define a record and create a fragmented table over nodes 1, 2 and 3. We start by starting the initial connection to node2 and node3 from node1 in the erlang shell through net:ping/1. Then store the list of nodes in a variable and pass it to mnesia:create_schema/1. Then, on each node we start the mnesia application through mnesia:start/0.

erl1 erl&gt; net:ping('node2@icedcoffee').
 pong
erl1 erl&gt; net:ping('node3@icedcoffee').
 pong
erl1 erl&gt; StorageNodes = [node() | nodes()].
 [node1@icedcoffee, node2@icedcoffee, node3@icedcoffee]
erl1 erl&gt; mnesia:create_schema(StorageNodes).
ok

erl1 erl&gt; mnesia:start().
 ok

erl2 erl&gt; mnesia:start().
 ok

erl3 erl&gt; mnesia:start().
 ok
</code></pre>

<p>Once mnesia is running on our grid we can verify that everything is running as expected using mnesia:system_info/1 and mnesia:info/0.</p>

<pre><code>erl1 erl&gt; mnesia:system_info(running_db_nodes).
 [node1@icedcoffee, node2@icedcoffee, node3@icedcoffee]
erl1 erl&gt; mnesia:info().
 ...
 [{'node1@icedcoffee',disc_copies},
  {'node2@icedcoffee',disc_copies},
  {'node3@icedcoffee',disc_copies}] = [schema]
 ...
</code></pre>

<p>At this point the initial fragmented mnesia table needs to be created and verified across these three nodes. We start by using rd/1 to define a record and then mnesia:create_table/2 to create the table. The table in this demonstration is a simple key/value pair dictionary. When we create our table will will create one fragment for each node (3 fragments) and set the table as disc-only.</p>

<pre><code>erl1 erl&gt; rd(dictionary, {key, value}).
 dictionary
erl1 erl&gt; FragProps = [{node_pool, StorageNodes}, {n_fragments, 3}, {n_disc_only_copies, 1}].
 [{node_pool,[node1@icedcoffee, node2@icedcoffee, node3@icedcoffee]}, {n_fragments,3}, {n_disc_only_copies,1}]
erl1 erl&gt; mnesia:create_table(dictionary, [{frag_properties, FragProps}, {attributes, record_info(fields, dictionary)}]).
 {atomic,ok}
</code></pre>

<p>We can verify that the table was created using mnesia:table_info/2. Then verify that each node has one fragment of the table. That is done with mnesia:info/0 and mnesia:table_info/2.</p>

<pre><code>erl1 erl&gt; mnesia:table_info(dictionary, frag_properties).
 [{base_table,dictionary},
  {foreign_key,undefined},
  {hash_module,mnesia_frag_hash},
  {hash_state,{hash_state,3,2,1,phash2}},
  {n_fragments,3},
  {node_pool,[node1@icedcoffee, node2@icedcoffee, node3@icedcoffee]}]
erl1 erl&gt; mnesia:info().
 ...
 [{node1@icedcoffee,disc_only_copies}] = [dictionary_frag3]
 [{node2@icedcoffee,disc_only_copies}] = [dictionary_frag2]
 [{node3@icedcoffee,disc_only_copies}] = [dictionary]
 ...
erl1 erl&gt; Info = fun(Item) -&gt; mnesia:table_info(dictionary, Item) end.
 #Fun&lt;erl_eval.6.35866844&gt;
erl1 erl&gt; mnesia:activity(sync_dirty, Info, [frag_dist], mnesia_frag).
 [{node1@icedcoffee,1},
  {node2@icedcoffee,1},
  {node3@icedcoffee,1}]
</code></pre>

<p>The next step is to populate the table with data and verify the data distribution across the fragments and nodes.</p>

<pre><code>erl1 erl&gt; WriteFun = fun() -&gt; [mnesia:write({dictionary, K, -K}) || K &lt;- lists:seq(1, 30)], ok end.
 #Fun&lt;erl_eval.20.117942162&gt;
erl1 erl&gt; mnesia:activity(sync_dirty, WriteFun, mnesia_frag).
 ok
erl1 erl&gt; mnesia:activity(sync_dirty, Info, [frag_size], mnesia_frag).
 [{dictionary,3},{dictionary_frag2,15},{dictionary_frag3,12}]
</code></pre>

<p>At this point we have distributed mnesia table over a set of nodes. We've essentially trippled the number of transactions that we can handle compared to running a non distributed-fragmented mnesia store. But lets consider the situation that comes when we need to grow to another node, either for mnesia load or table size.</p>

<p>The demonstration continues by lighting an additional node.</p>

<pre><code>ic:~/tmp/erl4 $ erl -sname node4 -setcookie 622c84b69ee448a07d80de5cbeb13e3d
</code></pre>

<p>What we do now is start mnesia on node4 and then reconfigure node1 to recognize node4.</p>

<pre><code>erl4 erl&gt; mnesia:start().
 ok

erl1 erl&gt; net:ping('node4@icedcoffee').
 pong
erl1 erl&gt; mnesia:change_config(extra_db_nodes, [node4@icedcoffee]).
 {ok,['node4@icedcoffee']}
</code></pre>

<p>On node4 when we run mnesia:info/0 we will now see all of the other nodes on our grid.</p>

<pre><code>erl4 erl&gt; mnesia:info().
 ...
 [{'node1@icedcoffee',disc_copies},
  {'node2@icedcoffee',disc_copies},
  {'node3@icedcoffee',disc_copies},
  {'node4@icedcoffee',ram_copies}] = [schema]
 ...
</code></pre>

<p>What you'll notice is that the schema on node4 is still a ram copy. This isn't compatible with the disc-only table setup we have on our other nodes. Before we can continue adding the node to the table schema, we need to transform that using mnesia:change_table_copy_type/3.</p>

<pre><code>erl4 erl&gt; mnesia:change_table_copy_type(schema, node(), disc_copies).
 {atomic,ok}
erl4 erl&gt; mnesia:info().
 ...
 [{'node1@icedcoffee',disc_copies},
  {'node2@icedcoffee',disc_copies},
  {'node3@icedcoffee',disc_copies},
  {'node4@icedcoffee',disc_copies}] = [schema]
 ...
</code></pre>

<p>Now that all of the nodes are in order we can add the node to the dictionary table schema and add another fragment to the table. This is done with mnesia:change_table_frag/2 calling the add_node and add_frag forms of this function.</p>

<pre><code>erl1 erl&gt; mnesia:change_table_frag(dictionary, {add_node, 'node4@icedcoffee'}).
 {atomic,ok}
erl1 erl&gt; NodeLayout = mnesia:activity(sync_dirty, Info, [frag_dist], mnesia_frag).
 [{'node4@icedcoffee',0},
  {'node1@icedcoffee',1},
  {'node2@icedcoffee',1},
  {'node3@icedcoffee',1}]
erl1 erl&gt; mnesia:change_table_frag(dictionary, {add_frag, NodeLayout}).
  {atomic,ok}
</code></pre>

<p>On the initial call to mnesia:change_table_frag/2 the second parameter is the {add_node, 'node4@icedcoffee'} tuple. The second call creates a list that represents the node layout that is then fed into the third call to mnesia:change_table_frag/2 that sends the {add_frag, NodeLayout} tuple telling mnesia to add another table fragment to the table. With that done we can see that our table has been adjusted and rebalanced accordingly.</p>

<pre><code>erl1 erl&gt; mnesia:activity(sync_dirty, Info, [frag_dist], mnesia_frag).
 [{'node1@icedcoffee',1},
  {'node2@icedcoffee',1},
  {'node3@icedcoffee',1},
  {'node4@icedcoffee',1}]
erl1 erl&gt; mnesia:activity(sync_dirty, Info, [frag_size], mnesia_frag).
 [{dictionary,3},
  {dictionary_frag2,8},
  {dictionary_frag3,12},
  {dictionary_frag4,7}]
</code></pre>

<p>If we wanted to add a fragment to a specific node we use the mnesia:change_table_frag/2 function but instead of passing a list we pass the node that we want to adjust.</p>

<pre><code>erl1 erl&gt; mnesia:change_table_frag(dictionary, {add_frag, ['node4@icedcoffee']}).
  {atomic,ok}
erl1 erl&gt; mnesia:change_table_frag(dictionary, {add_frag, ['node4@icedcoffee']}).
  {atomic,ok}
erl1 erl&gt; mnesia:change_table_frag(dictionary, {add_frag, ['node4@icedcoffee']}).
  {atomic,ok}
erl1 erl&gt; mnesia:activity(sync_dirty, Info, [frag_dist], mnesia_frag).
 [{'node1@icedcoffee',1},
  {'node2@icedcoffee',1},
  {'node3@icedcoffee',1},
  {'node4@icedcoffee',4}]
erl1 erl&gt; mnesia:activity(sync_dirty, Info, [frag_size], mnesia_frag).
 [{dictionary,1},
  {dictionary_frag2,6},
  {dictionary_frag3,6},
  {dictionary_frag4,7},
  {dictionary_frag5,2},
  {dictionary_frag6,2},
  {dictionary_frag7,6}]
</code></pre>

		</div>
		<div id="sidebar">
	<div class="clear"></div>
	<div id="sidebar_content">
		<h2>Recently</h2>
		
			<h3 style="padding-top: 10px;"><a alt="Integrating Heman into CalendERL About Nothing" href="/2009/12/28/integrating_heman_into_calenderl_about_nothing.html">Integrating Heman into CalendERL About Nothing</a></h3>
			<em id="sub_h3">28 December 2009</em>
		
			<h3 style="padding-top: 10px;"><a alt="Lets create some Erlang standards, part one" href="/2009/11/24/lets_create_some_erlang_standards_part_one.html">Lets create some Erlang standards, part one</a></h3>
			<em id="sub_h3">24 November 2009</em>
		
			<h3 style="padding-top: 10px;"><a alt="2009 Erlang User Conference" href="/2009/11/12/EUC2009.html">2009 Erlang User Conference</a></h3>
			<em id="sub_h3">12 November 2009</em>
		
			<h3 style="padding-top: 10px;"><a alt="CalendERL About Nothing" href="/2009/11/01/calenderl_about_nothing.html">CalendERL About Nothing</a></h3>
			<em id="sub_h3">01 November 2009</em>
		
			<h3 style="padding-top: 10px;"><a alt="Globally Shared Queues" href="/2009/10/26/globally_shared_queues.html">Globally Shared Queues</a></h3>
			<em id="sub_h3">26 October 2009</em>
		
		<h2 style="padding-top: 20px;"><a href="/about/">About</a></h2>
		<h2 style="padding-top: 20px;"><a href="/projects/">Projects</a></h2>
		<h2 style="padding-top: 20px;"><a href="/archives/">Archives</a></h2>
	</div>
</div>

		<div id="disqus_thread"></div>
	</div>
	<script type="text/javascript" src="http://disqus.com/forums/socklabs-blog/embed.js"></script>
<noscript>
	<a href="http://disqus.com/forums/socklabs-blog/?url=ref">View the discussion thread.</a>
</noscript>
<a href="http://disqus.com" class="dsq-brlink">blog comments powered by <span class="logo-disqus">Disqus</span></a>
<script type="text/javascript">
//<![CDATA[
(function() {
	var links = document.getElementsByTagName('a');
	var query = '?';
	for(var i = 0; i < links.length; i++) {
	if(links[i].href.indexOf('#disqus_thread') >= 0) {
		query += 'url' + i + '=' + encodeURIComponent(links[i].href) + '&';
	}
	}
	document.write('<script charset="utf-8" type="text/javascript" src="http://disqus.com/forums/socklabs-blog/get_num_replies.js' + query + '"></' + 'script>');
})();
//]]>
</script>

	<div class="clear"></div>
	<div id="footer">
	<a href="/" alt="blog"><b>Blog</b></a>
	| <a href="http://socklabs.com">Socklabs</a>
	| <a href="http://twitter.com/ngerakines" alt="status">On Twitter</a>
	| <a alt="ngerakines on Facebook" href="http://facebook.com/ngerakines">on Facebook</a>
</div>
	</body>
</html>